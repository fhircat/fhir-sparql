# QueryAnalyzer.getArcTrees() test 1
# Reflect order of final ArcTrees.

PREFIX fhir: <http://hl7.org/fhir/>
PREFIX sct: <http://snomed.info/id/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?obs ?patId WHERE {
  # Observation/Procedure/....code
  ?obs fhir:code ?code .					#  1
    ?code fhir:coding ?codeList .				#  2
      ?codeList rdf:rest*/rdf:first ?codeElt .			#  3
        ?codeElt fhir:code ?codeCode .				#  4
          ?codeCode fhir:v "72166-2" .				#  5
        ?codeElt fhir:system ?codingSystem .			#  6
          ?codingSystem fhir:v "http://loinc.org"^^xsd:anyURI .	#  7

  # Observation/Procedure/....subject
  ?obs fhir:subject ?subjectRef .				#  8
    ?subjectRef fhir:reference ?subjectBNode .			#  9
      ?subjectBNode fhir:link ?patRsrc .			# 10

  # Patient
  ?patRsrc a fhir:Patient .					# 11

  # Patient.id
  ?patRsrc fhir:id ?patIdElt .					# 12
    ?patIdElt fhir:v ?patId .					# 13
}
